#!/usr/bin/env perl
use strict;
use warnings;
use File::Spec;
use File::Spec::Functions qw(abs2rel canonpath catfile updir);
use File::Basename qw(dirname);
use Cwd qw(realpath);

# Generates a .pot file from lib/gamedata/* files.

# 'monster', 'class', ...
my $type = shift or die;

my $file = realpath(catfile(dirname(__FILE__), updir, "lib", "gamedata", "$type.txt")) or die "cannot find lib/gamedata/$type.txt";
   $file = abs2rel($file);

open my $fh, "<", $file or die "Can't open $file: $!";

my $seen = {};

sub emit_msg_entry {
    my ($msgid) = @_;

    if (ref $msgid eq 'ARRAY') {
        next if $seen->{ join "\n", @$msgid }++;

        print "#: $file:$.\n";
        print qq(msgctxt "$type"\n);
        print qq(msgid ""\n);
        for (@$msgid) {
            s/"/\\"/g;
            print qq("$_"\n);
        }
        print qq(msgstr ""\n\n);
    } else {
        return if $seen->{$msgid}++;

        print "#: $file:$.\n";
        print qq(msgctxt "$type"\n);
        print qq(msgid "$msgid"\n);
        print qq(msgstr ""\n\n);
    }
}

while (<$fh>) {
    chomp;

    if (/^name:(.+)/) {
        next if $type eq 'blow_methods';
        next if $type eq 'blow_effects';
        next if $type eq 'player_timed';
        /^name:[^:]+:(.+)/ or next if $type eq 'object_base';
		emit_msg_entry($1);
    }

    if (/^act:(.+)/) {
        next if $type eq 'artifact';
		emit_msg_entry($1);
    }

    if (/^desc:(.+)/) {
        next if $type eq 'player_timed';

        my @msgid = ($1);
        while (<$fh>) {
            if (/^desc:(.+)/) {
                push @msgid, $1;
            } else {
                last;
            }
        }

		emit_msg_entry([@msgid]);
    }

    if (/^slot:.+?:(.+)$/) {
        emit_msg_entry($1);
    }

    if (/^grade:.:\d+:([^:]+):([^:]*)(?::([^:]*))?$/) {
        my ($grade_name, $msg1, $msg2) = ($1, $2, $3);

		for ($grade_name, $msg1, $msg2) {
            next unless length $_;
            emit_msg_entry($_);
        }
    }

    if (/^(?:on-end|on-increase):(.+)$/) {
        emit_msg_entry($1);
    }

    if ($type eq 'pain' && /^message:(.+)$/) {
        emit_msg_entry($1);
    }
}
